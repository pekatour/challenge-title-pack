Void Animate_Show() {
	Page.GetFirstChild("FRAME_MENU").RelativeScale = 0.0;
	AnimMgr.Add(Page.GetFirstChild("FRAME_MENU"), "<frame scale=\"1\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
}

Boolean IsNotAnimating(Integer _Start, Integer _End) {
	return _End == -1 && Now - _Start > 1000;
}

***Start***
***
declare Start = Now;
declare End = -1;
***

***Loop***
***
foreach(Event,PendingEvents) {
	switch(Event.Type) {
		case CMlScriptEvent::Type::EntrySubmit: {
		
		}
		case CMlScriptEvent::Type::MouseOver: {
			if(TextLib::StartsWith("QUAD_BUTTON",Event.ControlId)) {
				//Audio.PlaySoundEvent(CAudioManager::ELibSound::Checkpoint, 0, 1.0);
			}
			switch(Event.ControlId) {
				case "QUAD_SIGN": {
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN"), "<quad scale=\"1.1\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN_GLOW"), "<quad scale=\"2\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
				}
			}
		}
		case CMlScriptEvent::Type::MouseOut: {
			switch(Event.ControlId) {
				case "QUAD_SIGN": {
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN"), "<quad scale=\"1\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN_GLOW"), "<quad scale=\"1\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
				}
			}
		}
		case CMlScriptEvent::Type::MouseClick: {
			if(Event.ControlId == "QUAD_BUTTON_CREATECHALLENGE") {
				if(End == -1)
				SendCustomEvent("Button_CreateChallenge", []);
			}
			if(Event.ControlId == "QUAD_BUTTON_EXIT") {
				if(End == -1)
				SendCustomEvent("Button_Exit", []);
			}
		}
		
		case CMlScriptEvent::Type::PluginCustomEvent: {
			switch(Event.CustomEventType) {
				case "Show": {
					Start = Now;
					End = -1;
					(Page.GetFirstChild("LABEL_VERSION") as CMlLabel).Value = "";
					(Page.GetFirstChild("LABEL_MADEBY") as CMlLabel).Value = "";
					SlowSetText((Page.GetFirstChild("LABEL_VERSION") as CMlLabel),TextLib::Split(" ", LoadedTitle.TitleVersion)[0],Now+750,50);
					SlowSetText((Page.GetFirstChild("LABEL_MADEBY") as CMlLabel),"{{{{MADE_BY}}}} $i$o$nBIGBANG1112",Now+750,50);

					(Page.GetFirstChild("QUAD_SIGN") as CMlQuad).Opacity = 0.;
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN"), "<quad opacity=\"1\"/>", Now+1000, 300, CAnimManager::EAnimManagerEasing::QuadOut);
					(Page.GetFirstChild("QUAD_SIGN_GLOW") as CMlQuad).Opacity = 0.;
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN_GLOW"), "<quad opacity=\"1\"/>", Now+1000, 1000, CAnimManager::EAnimManagerEasing::QuadOut);
				}
				case "Hide": {
					End = Now;
					SlowSetText((Page.GetFirstChild("LABEL_VERSION") as CMlLabel),"",Now+750,50);
					SlowSetText((Page.GetFirstChild("LABEL_MADEBY") as CMlLabel),"",Now+750,50);

					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN"), "<quad opacity=\"0\"/>", 300, CAnimManager::EAnimManagerEasing::QuadOut);
					AnimMgr.Add(Page.GetFirstChild("QUAD_SIGN_GLOW"), "<quad opacity=\"0\"/>", 1000, CAnimManager::EAnimManagerEasing::QuadOut);
				}
			}
		}
	}
}

if(Now - Start <= 1000) {
	foreach(Control,(Page.GetFirstChild("FRAME_BACKGROUND") as CMlFrame).Controls) {
		declare Quad = (Control as CMlQuad);
		Quad.Size.X = AnimLib::EaseOutQuad(Now - Start - 500, 1.0, 110.0, 500);
		Quad.Size.Y = AnimLib::EaseOutQuad(Now - Start, 1.0, 50.0, 500);
	}

	(Page.GetFirstChild("FRAME_INSIDE") as CMlFrame).ClipWindowSize.X = AnimLib::EaseOutQuad(Now - Start - 500, 0.0, 100.0, 500);
	(Page.GetFirstChild("FRAME_INSIDE") as CMlFrame).ClipWindowSize.Y = AnimLib::EaseOutQuad(Now - Start, 0.0, 50.0, 500);
}

(Page.GetFirstChild("QUAD_BUTTON_CREATECHALLENGE_FLASH") as CMlQuad).Opacity
= (AnimLib::EaseOutQuad(Now - Start - 500, .0, 1.0, 200) - AnimLib::EaseOutQuad(Now - Start - 500 - 200, .0, 1.0, 200))*.75;
(Page.GetFirstChild("QUAD_BUTTON_REPLAYS_FLASH") as CMlQuad).Opacity
= (AnimLib::EaseOutQuad(Now - Start - 500 - 200, .0, 1.0, 200) - AnimLib::EaseOutQuad(Now - Start - 500 - 400, .0, 1.0, 200))*.75;
(Page.GetFirstChild("QUAD_BUTTON_EXIT_FLASH") as CMlQuad).Opacity
= (AnimLib::EaseOutQuad(Now - Start - 500 - 400, .0, 1.0, 200) - AnimLib::EaseOutQuad(Now - Start - 500 - 600, .0, 1.0, 200))*.75;

if(End != -1) {
	foreach(Control,(Page.GetFirstChild("FRAME_BACKGROUND") as CMlFrame).Controls) {
		declare Quad = (Control as CMlQuad);
		Quad.Size.X = AnimLib::EaseOutQuad(Now - End, 110.0, -109.0, 500);
		Quad.Size.Y = AnimLib::EaseOutQuad(Now - End - 500, 50.0, -49.0, 500);
	}

	(Page.GetFirstChild("FRAME_INSIDE") as CMlFrame).ClipWindowSize.X = AnimLib::EaseOutQuad(Now - End, 100.0, -100.0, 500);
	(Page.GetFirstChild("FRAME_INSIDE") as CMlFrame).ClipWindowSize.Y = AnimLib::EaseOutQuad(Now - End - 500, 50.0, -50.0, 500);

	if(Now - End > 1000) {
		SendCustomEvent("Hide_Response", []);
		End = -1;
	}
}
***